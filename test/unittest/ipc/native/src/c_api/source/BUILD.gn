# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("../../../../../../../config.gni")

IPC_CORE_ROOT = "$IPC_ROOT_PATH/ipc/native"
MODULE_OUTPUT_PATH = "ipc"

template("c_api_unittest") {
  ohos_unittest(target_name) {
    module_out_path = MODULE_OUTPUT_PATH

    include_dirs = [
      "$IPC_CORE_ROOT/c/rpc/include",
      "$IPC_CORE_ROOT/c/ipc_adapter/include",
      "$IPC_CORE_ROOT/src/core/dbinder/include",
      "$IPC_CORE_ROOT/src/core/framework/include",
      "$IPC_CORE_ROOT/src/core/invoker/include",
      "$IPC_CORE_ROOT/src/c_api/include",
      "$IPC_ROOT_PATH/interfaces/innerkits/c_api/include",
      "$IPC_ROOT_PATH/interfaces/innerkits/ipc_core/include",
      "$IPC_ROOT_PATH/interfaces/innerkits/libdbinder/include",
      "$IPC_ROOT_PATH/utils/include",
      "$IPC_UNITTEST_ROOT_PATH/mock",
    ]

    if (defined(invoker.include_dirs)) {
      include_dirs += invoker.include_dirs
    }

    configs = [ "$IPC_ROOT_PATH/config:ipc_util_config" ]

    deps = [
      "$IPC_CORE_ROOT/src/core:ipc_common",
      "$IPC_ROOT_PATH/interfaces/innerkits/c_api:ipc_capi",
      "$IPC_ROOT_PATH/interfaces/innerkits/ipc_core:ipc_core",
      "$IPC_ROOT_PATH/interfaces/innerkits/ipc_single:ipc_single_test",
    ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    external_deps = [
      "c_utils:utils",
      "googletest:gmock",
      "hilog:libhilog",
      "hitrace:libhitracechain",
      "samgr:samgr_proxy",
    ]

    if (defined(invoker.external_deps)) {
      external_deps += invoker.external_deps
    }

    symlink_target_name = [ "libipc_capi.so" ]

    sources = invoker.sources
    ldflags = [ "-lpthread" ]
    resource_config_file = "$IPC_ROOT_PATH/test/resource/ipc/ohos_test.xml"
  }
}

c_api_unittest("IPCInternalUtilsTest") {
  sources = [
    "$IPC_CORE_ROOT/src/c_api/source/ipc_internal_utils.cpp",
    "ipc_internal_utils_unittest.cpp",
  ]
}

c_api_unittest("IPCCskeletonTest") {
  sources = [
    "$IPC_CORE_ROOT/src/c_api/source/ipc_cskeleton.cpp",
    "ipc_cskeleton_unittest.cpp",
  ]
}

c_api_unittest("IPCCremoteObjectTest") {
  sources = [ "ipc_cremote_object_unittest.cpp" ]
}

c_api_unittest("IPCInnerObjectTest") {
  sources = [ "ipc_inner_object_unittest.cpp" ]
}

c_api_unittest("IPCCparcelTest") {
  sources = [ "ipc_cparcel_unittest.cpp" ]
}

###############################################################################
group("unittest") {
  testonly = true
  deps = [
    ":IPCCparcelTest",
    ":IPCCremoteObjectTest",
    ":IPCCskeletonTest",
    ":IPCInnerObjectTest",
    ":IPCInternalUtilsTest",
  ]
}
###############################################################################
