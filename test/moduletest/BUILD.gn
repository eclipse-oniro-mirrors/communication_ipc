# Copyright (C) 2021-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")

SUBSYSTEM_DIR = "../../"
IPC_TEST_ROOT = "$SUBSYSTEM_DIR/ipc/test"
IPC_CORE_ROOT = "$SUBSYSTEM_DIR/ipc/native"
MODULE_OUTPUT_PATH = "ipc"

template("ipc_moduletest") {
  ohos_moduletest(target_name) {
    module_out_path = MODULE_OUTPUT_PATH

    configs = [
      "$SUBSYSTEM_DIR/config:ipc_util_config",
      "$SUBSYSTEM_DIR/config:dsoftbus_header_deps_config",
    ]
    include_dirs = [
      "$IPC_CORE_ROOT/c/rpc/include",
      "$IPC_CORE_ROOT/c/ipc_adapter/include",
      "$IPC_CORE_ROOT/c/adapter/access_token/include",
      "$IPC_CORE_ROOT/src/core/include",
      "$IPC_CORE_ROOT/src/mock/include",
      "$SUBSYSTEM_DIR/interfaces/innerkits/c_api/include",
      "$SUBSYSTEM_DIR/interfaces/innerkits/ipc_core/include",
      "$SUBSYSTEM_DIR/interfaces/innerkits/libdbinder/include",
      "$SUBSYSTEM_DIR/utils/include",
      "$SUBSYSTEM_DIR/native/src/core/include",
      "$SUBSYSTEM_DIR/ipc/native/src/mock/include/",
      "$SUBSYSTEM_DIR/ipc/test/auxiliary/native/include/",
    ]

    if (defined(invoker.include_dirs)) {
      include_dirs += invoker.include_dirs
    }

    deps = [
      "$IPC_CORE_ROOT/src/core:ipc_common",
      "$IPC_TEST_ROOT/auxiliary/native:TestAssistance",
      "$SUBSYSTEM_DIR/interfaces/innerkits/c_api:ipc_capi",
      "$SUBSYSTEM_DIR/interfaces/innerkits/ipc_single:ipc_single",
    ]
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    external_deps = [
      "c_utils:utils",
      "googletest:gmock",
      "hilog:libhilog",
      "samgr:samgr_proxy",
    ]
    if (defined(invoker.external_deps)) {
      external_deps += invoker.external_deps
    }

    sources = invoker.sources
    ldflags = [ "-lpthread" ]

    resource_config_file = "$SUBSYSTEM_DIR/test/resource/ipc/ohos_test.xml"
  }
}

###############################################################################

ipc_moduletest("IPCCpiModuleTest") {
  sources = [ "c/ipc_capi_remote_object_module_test.cpp" ]
}

###############################################################################

group("moduletest") {
  testonly = true
  deps = [ ":IPCCpiModuleTest" ]
}
