# Copyright (C) 2021-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("../../../../../config.gni")

SUBSYSTEM_DIR = "//foundation/communication/ipc"
IPC_TEST_ROOT = "$SUBSYSTEM_DIR/ipc/test"
IPC_CORE_ROOT = "$SUBSYSTEM_DIR/ipc/native"
MODULE_OUTPUT_PATH = "ipc/ipc"

template("ipc_unittest") {
  ohos_unittest(target_name) {
    module_out_path = MODULE_OUTPUT_PATH

    configs = [
      "$SUBSYSTEM_DIR/config:ipc_util_config",
      "$SUBSYSTEM_DIR/config:dsoftbus_header_deps_config",
    ]
    include_dirs = [
      "$IPC_CORE_ROOT/c/rpc/include",
      "$IPC_CORE_ROOT/c/ipc_adapter/include",
      "$IPC_CORE_ROOT/c/adapter/access_token/include",
      "$IPC_CORE_ROOT/src/core/dbinder/include",
      "$IPC_CORE_ROOT/src/core/framework/include",
      "$IPC_CORE_ROOT/src/core/invoker/include",
      "$SUBSYSTEM_DIR/interfaces/innerkits/c_api/include",
      "$SUBSYSTEM_DIR/interfaces/innerkits/ipc_core/include",
      "$SUBSYSTEM_DIR/interfaces/innerkits/libdbinder/include",
      "$SUBSYSTEM_DIR/utils/include",
      "$SUBSYSTEM_DIR/native/src/core/include",
    ]

    if (defined(invoker.include_dirs)) {
      include_dirs += invoker.include_dirs
    }

    deps = [
      "$IPC_CORE_ROOT/src/core:ipc_common",
      "$SUBSYSTEM_DIR/interfaces/innerkits/c_api:ipc_capi",
      "$SUBSYSTEM_DIR/interfaces/innerkits/ipc_single:ipc_single_test",
    ]
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    external_deps = [
      "c_utils:utils",
      "googletest:gmock",
      "hilog:libhilog",
      "hitrace:libhitracechain",
      "samgr:samgr_proxy",
    ]

    if (hiviewdfx_hisysevent_support) {
      external_deps += [ "hisysevent:libhisysevent" ]
    }

    if (defined(invoker.external_deps)) {
      external_deps += invoker.external_deps
    }

    sources = invoker.sources
    ldflags = [ "-lpthread" ]

    resource_config_file = "$SUBSYSTEM_DIR/test/resource/ipc/ohos_test.xml"
  }
}

ipc_unittest("IPCNativeUnitTest") {
  sources = [
    "$IPC_CORE_ROOT/src/core/framework/source/ipc_payload_statistics_impl.cpp",
    "$IPC_CORE_ROOT/src/core/framework/source/process_skeleton.cpp",
    "$IPC_CORE_ROOT/src/core/framework/source/stub_refcount_object.cpp",
    "$IPC_CORE_ROOT/src/core/invoker/source/binder_connector.cpp",
    "buffer_object_unittest.cpp",
    "dbinder_callback_stub_unittest.cpp",
    "dbinder_session_object_unittest.cpp",
    "ipc_core_unittest.cpp",
    "ipc_object_proxy_unittest.cpp",
    "ipc_object_stub_unittest.cpp",
    "ipc_payload_statistics_impl_unittest.cpp",
    "ipc_payload_statistics_unittest.cpp",
    "ipc_process_skeleton_unittest.cpp",
    "ipc_skeleton_unittest.cpp",
    "ipc_thread_pool_unittest.cpp",
    "ipc_thread_skeleton_unittest.cpp",
    "iremote_object_unitest.cpp",
    "message_parcel_unittest.cpp",
    "process_skeleton_unittest.cpp",
  ]
}

ipc_unittest("IPCFileDescOpsTest") {
  sources = [ "ipc_file_desc_unittest.cpp" ]
}

ipc_unittest("BinderConnectorTest") {
  sources = [ "ipc_connector_unittest.cpp" ]
}

ipc_unittest("BinderDebugUnitTest") {
  sources = [
    "$IPC_CORE_ROOT/src/core/framework/source/binder_debug.cpp",
    "binder_debug_unittest.cpp",
  ]
}

ipc_unittest("BinderInvokerUnitTest") {
  sources = [ "binder_invoker_unittest.cpp" ]
}

ipc_unittest("InvokerFactoryTest") {
  sources = [ "invoker_factory_unittest.cpp" ]
}

ipc_unittest("RPCFeatureUnitTest") {
  sources = [ "rpc_feature_set_unittest.cpp" ]
}

ipc_unittest("IPCSoDlcloseTest") {
  sources = [ "ipc_so_dlclose_unittest.cpp" ]
}

ipc_unittest("IPCBlockThreadTest") {
  sources = [ "ipc_block_thread_unittest.cpp" ]
}

ipc_unittest("HitraceInvokerTest") {
  sources = [
    "$IPC_CORE_ROOT/src/core/framework/source/process_skeleton.cpp",
    "$IPC_CORE_ROOT/src/core/invoker/source/hitrace_invoker.cpp",
    "hitrace_invoker_unittest.cpp",
  ]
}

ipc_unittest("DatabusSocketListenerTest") {
  sources = [
    "$IPC_CORE_ROOT/src/core/dbinder/source/databus_socket_listener.cpp",
    "$IPC_CORE_ROOT/src/core/framework/source/process_skeleton.cpp",
    "$IPC_CORE_ROOT/src/core/invoker/source/binder_connector.cpp",
    "databus_socket_listener_unittest.cpp",
  ]
}

ipc_unittest("MessageParcelTest") {
  include_dirs = [ "$IPC_CORE_ROOT/src/core/framework/source" ]
  sources = [ "message_parcel_unittest_AcquireObject.cpp" ]
}

ipc_unittest("InvokerRawDataTest") {
  sources = [ "invoker_rawdata_unittest.cpp" ]
}

if (support_jsapi) {
  ipc_unittest("IPCNapiUnitTest") {
    sources = [ "ipc_napi_unittest.cpp" ]
    external_deps = [
      "bounds_checking_function:libsec_shared",
      "ipc:ipc_napi",
      "napi:ace_napi",
    ]
  }
}

ipc_unittest("IPCMockUnitTest") {
  sources = [ "ipc_binder_databus_invoker_unittest.cpp" ]
}

ohos_unittest("IpcCApiUnitTest") {
  module_out_path = MODULE_OUTPUT_PATH
  include_dirs = [ "$SUBSYSTEM_DIR/interfaces/innerkits/ipc_core/include" ]
  sources = [
    "ipc_capi_parcel_unittest.cpp",
    "ipc_capi_remote_object_unittest.cpp",
    "ipc_capi_skeleton_unittest.cpp",
  ]

  configs = [
    "$SUBSYSTEM_DIR/config:ipc_util_config",
    "$IPC_TEST_ROOT:ipc_test_config",
  ]

  deps = [
    "$SUBSYSTEM_DIR/interfaces/innerkits/c_api:ipc_capi",
    "$SUBSYSTEM_DIR/interfaces/innerkits/ipc_single:ipc_single",
  ]

  external_deps = [
    "c_utils:utils",
    "googletest:gmock",
    "hilog:libhilog",
    "hitrace:libhitracechain",
    "samgr:samgr_proxy",
  ]

  if (hiviewdfx_hisysevent_support) {
    external_deps += [ "hisysevent:libhisysevent" ]
  }

  resource_config_file = "$SUBSYSTEM_DIR/test/resource/ipc/ohos_test.xml"
}

ohos_unittest("IPCTraceUnitTest") {
  module_out_path = MODULE_OUTPUT_PATH

  include_dirs = [ "$SUBSYSTEM_DIR/ipc/native/src/core/include" ]

  sources = [
    "$SUBSYSTEM_DIR/ipc/native/src/core/framework/source/ipc_trace.cpp",
    "ipc_trace_unittest.cpp",
  ]

  configs = [ "$IPC_TEST_ROOT:ipc_test_config" ]

  external_deps = [ "hilog:libhilog" ]

  resource_config_file = "$SUBSYSTEM_DIR/test/resource/ipc/ohos_test.xml"
}

###############################################################################
group("unittest") {
  testonly = true
  deps = [
    ":BinderConnectorTest",
    ":BinderDebugUnitTest",
    ":BinderInvokerUnitTest",
    ":DatabusSocketListenerTest",
    ":HitraceInvokerTest",
    ":IPCBlockThreadTest",
    ":IPCFileDescOpsTest",
    ":IPCMockUnitTest",
    ":IPCNativeUnitTest",
    ":IPCSoDlcloseTest",
    ":IPCTraceUnitTest",
    ":InvokerFactoryTest",
    ":InvokerRawDataTest",
    ":IpcCApiUnitTest",
    ":MessageParcelTest",
    ":RPCFeatureUnitTest",
  ]
  if (support_jsapi) {
    deps += [ ":IPCNapiUnitTest" ]
  }
}
###############################################################################
